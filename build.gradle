import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'io.github.0ffz.github-packages'
}

group 'com.derongan.minecraft'
version plugin_version

if (project.hasProperty("buildNo")) version += ".$buildNo"

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    withSourcesJar()
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://erethon.de/repo/' } //HeadLib
    maven githubPackage.invoke("MineInAbyss/guiy")
    maven githubPackage.invoke("MineInAbyss/DeeperWorld")
    maven githubPackage.invoke("MineInAbyss/Idofront")
    maven githubPackage.invoke("MineInAbyss/geary")
    maven { url 'https://jitpack.io' } //JitPack
}

publishing {
    repositories {
        maven githubPackage.invoke("MineInAbyss/MineInAbyss")
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$server_version"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
//    compileOnly 'com.badlogicgames.ashley:ashley:1.7.3'

    implementation 'de.erethon:headlib:3.0.4'

    implementation 'com.mineinabyss:geary:1.0.+'
    implementation 'com.derongan.minecraft:guiy:0.1.+'
    implementation 'com.mineinabyss:idofront:0.3.24'
    compileOnly 'com.derongan.minecraft:deeperworld:0.1.4-alpha.21'

}

processResources {
    filter ReplaceTokens, tokens: [plugin_version: plugin_version]
}

shadowJar {
    getArchiveClassifier().set(null)
    relocate 'com.derongan.minecraft.guiy', "com.derongan.minecraft.guiy.${project.group}.${project.name}"
    relocate 'com.mineinabyss.idofront', "com.mineinabyss.idofront.${project.group}.${project.name}"
    minimize {
        exclude(dependency('de.erethon:headlib:.*'))
    }
}
build.dependsOn shadowJar

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }

//Move into plugins folder
if (project.hasProperty("plugin_path") && plugin_path) {
    println("Copying to plugin directory $plugin_path")
    task copyJar(type: Copy) {
        from shadowJar // here it automatically reads jar file produced from jar task
        into plugin_path
    }
    build.dependsOn copyJar
    if (useLocalDeeperWorld.toBoolean())
        build.dependsOn gradle.includedBuild("DeeperWorld").task(':copyJar')
}
